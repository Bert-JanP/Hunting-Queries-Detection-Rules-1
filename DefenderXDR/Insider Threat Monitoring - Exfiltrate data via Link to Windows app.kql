// Insider Threat Monitoring - Exfiltrate data via Link to Windows app
// https://www.linkedin.com/posts/activity-7222581799716016128-7Nww/

// Microsoft has simplified accessing Android phones from a PC on Windows 11. However, this convenience could potentially allow critical identities to exfiltrate data, as it lacks logging via browser or network access. By utilizing DefenderXDR exposure management, the following KQL can detect the installation of the ‘Link to Windows’ app on endpoints associated with critical identities for DLP monitoring.

let CriticalIdentities =
ExposureGraphNodes
| where set_has_element(Categories, "identity")
| where isnotnull(NodeProperties.rawData.criticalityLevel) and
NodeProperties.rawData.criticalityLevel.criticalityLevel < 4 
| distinct NodeName;
let CriticalDevices =
ExposureGraphEdges 
| where EdgeLabel == @"can authenticate to"
| join ExposureGraphNodes on $left.TargetNodeId==$right.NodeId
| extend DName = tostring(NodeProperties.rawData.deviceName)
| extend isLocalAdmin = EdgeProperties.rawData.userRightsOnDevice.isLocalAdmin
| where SourceNodeName has_any (CriticalIdentities)
| distinct DName;
DeviceFileEvents
| where Timestamp > ago(30d)
| where ActionType == "FileCreated"
| where FileName contains "YourPhoneAppProxy"
| where DeviceName has_any (CriticalDevices)

// MITRE ATT&CK Mapping

// The KQL query is designed to identify critical identities and devices, and then monitor for specific file creation events on those devices. This approach helps in detecting potential unauthorized access and suspicious activities, aligning with several MITRE ATT&CK techniques such as Valid Accounts (T1078), Permission Groups Discovery (T1069), Ingress Tool Transfer (T1105), and Application Layer Protocol (T1071).

// T1078: Valid Accounts - Identifying critical accounts that could be targeted for unauthorized access.
// T1078: Valid Accounts - Identifying devices that critical accounts can access.
// T1069: Permission Groups Discovery - Discovering devices and their associated permissions.
// T1105: Ingress Tool Transfer - Detecting the creation of files that might be used for transferring tools or data.
// T1071: Application Layer Protocol - Monitoring for suspicious file creation that could indicate data exfiltration or command and control.
