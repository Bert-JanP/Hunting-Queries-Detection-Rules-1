// Analyzing Malicious Microsoft Graph API Rate Limit Count
// https://www.linkedin.com/posts/activity-7218488532250546178-zAMf/

// Utilizing Sentinelâ€™s BehaviourAnalytics with MicrosoftGraphActivityLogs to pinpoint Entra app IDs that are abusing the Microsoft Graph API and causing throttling due to malicious activities. Once the throttling operation resource is identified, additional KQLs can be further deployed to detect specific use case misuse. ðŸŽ¯

let AttackerIPs =
BehaviorAnalytics
| where TimeGenerated > ago(90d)
| extend ThreatIntelIndicatorDescription = tostring(DevicesInsights.ThreatIntelIndicatorDescription)
| where ThreatIntelIndicatorDescription contains "proxy"
| distinct SourceIPAddress;
MicrosoftGraphActivityLogs 
| where TimeGenerated > ago(90d) 
| where IPAddress has_any(AttackerIPs)
| extend path = replace_string(replace_string(replace_regex(tostring(parse_url(RequestUri).Path), @'(\/)+','//'),'v1.0/',''),'beta/','') 
| extend UriSegments = extract_all(@'\/([A-z2]+|\$batch)($|\/|\(|\$)',dynamic([1]),tolower(path))
| extend OperationResource = strcat_array(UriSegments,'/')| summarize RateLimitedCount=count() by AppId, OperationResource, RequestMethod 
| sort by RateLimitedCount desc

// MITRE ATT&CK Mapping

// T1071.001 - Application Layer Protocol: Web Protocols
// The query looks for suspicious activity in web protocols, specifically targeting proxy-related indicators.
// T1070.004 - Indicator Removal on Host: File Deletion
// By analyzing logs for specific IPs and paths, the query can help detect attempts to remove or hide indicators on the host.
// T1087.001 - Account Discovery: Local Account
// The query extends and normalizes paths to identify operations on resources, which can be related to account discovery activities.
// T1040 - Network Sniffing
// The use of BehaviorAnalytics to identify proxy-related activities can be linked to network sniffing techniques.
// T1078 - Valid Accounts
// By summarizing and sorting activities by AppId and RequestMethod, the query can help detect the use of valid accounts for malicious purposes.
